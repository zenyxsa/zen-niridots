pragma Singleton
pragma ComponentBehavior: Bound

import qs.modules.common
import qs.services
import Quickshell;
import QtQuick;

/**
 * A service for interacting with various booru APIs.
 */
Singleton {
    id: root

    function _log(...args): void {
        if (Quickshell.env("QS_DEBUG") === "1") console.log(...args);
    }

    property Component booruResponseDataComponent: BooruResponseData {}

    signal tagSuggestion(string query, var suggestions)
    signal responseFinished()

    property string failMessage: Translation.tr("That didn't work. Tips:\n- Check your tags and NSFW settings\n- If you don't have a tag in mind, type a page number")
    property var responses: []
    readonly property int responseLimit: 20
    property int runningRequests: 0

    function _destroyResponsesLater(items) {
        const doomed = (items || []).filter(item => item !== null && item !== undefined)
        if (doomed.length === 0)
            return
        Qt.callLater(() => {
            for (let i = 0; i < doomed.length; ++i) {
                if (typeof doomed[i].destroy === "function")
                    doomed[i].destroy()
            }
        })
    }

    function _appendResponse(response) {
        const next = [...root.responses, response]
        const removed = []
        while (next.length > root.responseLimit)
            removed.push(next.shift())
        root.responses = next
        root._destroyResponsesLater(removed)
    }
    property var defaultUserAgent: Config.options?.networking?.userAgent || "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/123.0.0.0 Safari/537.36"
    property var providerList: Object.keys(providers).filter(provider => provider !== "system" && providers[provider].api)
    property var providers: {
        "system": { "name": Translation.tr("System") },
        "yandere": {
            "name": "yande.re",
            "url": "https://yande.re",
            "api": "https://yande.re/post.json",
            "description": Translation.tr("All-rounder | Good quality, decent quantity"),
            "mapFunc": (response) => {
                return response.map(item => {
                    return {
                        "id": item.id,
                        "width": item.width,
                        "height": item.height,
                        "aspect_ratio": item.width / item.height,
                        "tags": item.tags,
                        "rating": item.rating,
                        "is_nsfw": (item.rating != 's'),
                        "md5": item.md5,
                        "preview_url": item.preview_url,
                        "sample_url": item.sample_url ?? item.file_url,
                        "file_url": item.file_url,
                        "file_ext": item.file_ext,
                        "source": getWorkingImageSource(item.source) ?? item.file_url,
                    }
                })
            },
            "tagSearchTemplate": "https://yande.re/tag.json?order=count&limit=10&name={{query}}*",
            "tagMapFunc": (response) => {
                return response.map(item => {
                    return {
                        "name": item.name,
                        "count": item.count
                    }
                })
            }
        },
        "konachan": {
            "name": "Konachan",
            "url": "https://konachan.net",
            "api": "https://konachan.net/post.json",
            "description": Translation.tr("For desktop wallpapers | Good quality"),
            "mapFunc": (response) => {
                return response.map(item => {
                    return {
                        "id": item.id,
                        "width": item.width,
                        "height": item.height,
                        "aspect_ratio": item.width / item.height,
                        "tags": item.tags,
                        "rating": item.rating,
                        "is_nsfw": (item.rating != 's'),
                        "md5": item.md5,
                        "preview_url": item.preview_url,
                        "sample_url": item.sample_url ?? item.file_url,
                        "file_url": item.file_url,
                        "file_ext": item.file_ext,
                        "source": getWorkingImageSource(item.source) ?? item.file_url,
                    }
                })
            },
            "tagSearchTemplate": "https://konachan.net/tag.json?order=count&limit=10&name={{query}}*",
            "tagMapFunc": (response) => {
                return response.map(item => {
                    return {
                        "name": item.name,
                        "count": item.count
                    }
                })
            }
        },
        "zerochan": {
            "name": "Zerochan",
            "url": "https://www.zerochan.net",
            "api": "https://www.zerochan.net/?json",
            "description": Translation.tr("Clean stuff | Excellent quality, no NSFW"),
            "mapFunc": (response) => {
                response = response.items
                return response.map(item => {
                    return {
                        "id": item.id,
                        "width": item.width,
                        "height": item.height,
                        "aspect_ratio": item.width / item.height,
                        "tags": item.tags.join(" "),
                        "rating": "s", // Zerochan doesn't have nsfw
                        "is_nsfw": false,
                        "md5": item.md5,
                        "preview_url": item.thumbnail,
                        "sample_url": item.thumbnail,
                        "file_url": item.thumbnail,
                        "file_ext": "avif",
                        "source": getWorkingImageSource(item.source) ?? item.thumbnail,
                        "character": item.tag
                    }
                })
            }
        },
        "danbooru": {
            "name": "Danbooru",
            "url": "https://danbooru.donmai.us",
            "api": "https://danbooru.donmai.us/posts.json",
            "description": Translation.tr("The popular one | Best quantity, but quality can vary wildly"),
            "mapFunc": (response) => {
                return response.map(item => {
                    return {
                        "id": item.id,
                        "width": item.image_width,
                        "height": item.image_height,
                        "aspect_ratio": item.image_width / item.image_height,
                        "tags": item.tag_string,
                        "rating": item.rating,
                        "is_nsfw": (item.rating != 's'),
                        "md5": item.md5,
                        "preview_url": item.preview_file_url,
                        "sample_url": item.file_url ?? item.large_file_url,
                        "file_url": item.large_file_url,
                        "file_ext": item.file_ext,
                        "source": getWorkingImageSource(item.source) ?? item.file_url,
                    }
                })
            },
            "tagSearchTemplate": "https://danbooru.donmai.us/tags.json?limit=10&search[name_matches]={{query}}*",
            "tagMapFunc": (response) => {
                return response.map(item => {
                    return {
                        "name": item.name,
                        "count": item.post_count
                    }
                })
            }
        },
        "gelbooru": {
            "name": "Gelbooru",
            "url": "https://gelbooru.com",
            "api": "https://gelbooru.com/index.php?page=dapi&s=post&q=index&json=1",
            "description": Translation.tr("The hentai one | Great quantity, a lot of NSFW, quality varies wildly"),
            "mapFunc": (response) => {
                response = response.post
                return response.map(item => {
                    return {
                        "id": item.id,
                        "width": item.width,
                        "height": item.height,
                        "aspect_ratio": item.width / item.height,
                        "tags": item.tags,
                        "rating": item.rating.replace('general', 's').charAt(0),
                        "is_nsfw": (item.rating != 's'),
                        "md5": item.md5,
                        "preview_url": item.preview_url,
                        "sample_url": item.sample_url ?? item.file_url,
                        "file_url": item.file_url,
                        "file_ext": item.file_url.split('.').pop(),
                        "source": getWorkingImageSource(item.source) ?? item.file_url,
                    }
                })
            },
            "tagSearchTemplate": "https://gelbooru.com/index.php?page=dapi&s=tag&q=index&json=1&orderby=count&limit=10&name_pattern={{query}}%",
            "tagMapFunc": (response) => {
                return response.tag.map(item => {
                    return {
                        "name": item.name,
                        "count": item.count
                    }
                })
            }
        },

        // https://docs.waifu.im/docs/getting-started
        "waifu.im": {
            "name": "waifu.im",
            "url": "https://waifu.im",
            "api": "https://api.waifu.im/images/",
            "description": Translation.tr("Waifus only | Excellent quality, limited quantity"),
            "mapFunc": (response) => {
                response = response.items
                return response.map(item => {
                    return {
                        "id": item.id,
                        "width": item.width,
                        "height": item.height,
                        "aspect_ratio": item.width / item.height,
                        "tags": item.tags.map(tag => {return tag.name}).join(" "),
                        "rating": item.isNsfw ? "e" : "s",
                        "is_nsfw": item.isNsfw,
                        "md5": Qt.md5(item.perceptualHash),
                        "preview_url": item.url,
                        "sample_url": item.url,
                        "file_url": item.url,
                        "file_ext": item.extension,
                        "source": item.source ? getWorkingImageSource(item.source) : item.url, // source sometimes might be null, so we gotta take care of it
                    }
                })
            },
            "tagSearchTemplate": "https://api.waifu.im/tags?Name={{query}}",
            "tagMapFunc": (response) => {
                return response.items.map(tag => ({ "name": tag.slug }))
            }
        },

        // https://t.alcy.cc/docs.html
        "t.alcy.cc": {
            "name": "Alcy",
            "url": "https://t.alcy.cc",
            "api": "https://t.alcy.cc/",
            "description": Translation.tr("Large images | God tier quality, no NSFW."),
            "fixedTags": [
                {
                    "name": "pc",
                    "count": "General"
                },
                {
                    "name": "moe",
                    "count": "Moe"
                },
                {
                    "name": "ys",
                    "count": "Genshin Impact"
                },
                {
                    "name": "tx",
                    "count": "General"
                },
                {
                    "name": "lai",
                    "count": "Menhera-chan"
                },
                {
                    "name": "fj",
                    "count": "Landscape"
                },
                {
                    "name": "bd",
                    "count": "Girl on white background"
                },
                {
                    "name": "xhl",
                    "count": "Shiggy"
                },
            ], 
            "mapFunc": (response) => {
                let items = response.data
                if (!Array.isArray(items)) items = [items]
                return items.map(item => {
                    return {
                        "id": item.id,
                        "width": 1920,
                        "height": 1080,
                        "aspect_ratio": 1920 / 1080,
                        "tags": "[no tags]",
                        "rating": "s",
                        "is_nsfw": false,
                        "md5": Qt.md5(item.link),
                        "preview_url": item.link,
                        "sample_url": item.link,
                        "file_url": item.link,
                        "file_ext": item.link.split('.').pop(),
                        "source": item.link
                    }
                })
            },
        }
    }
    property var currentProvider: Persistent.states.booru.provider

    function getWorkingImageSource(url) {
        if (url.includes('pximg.net')) {
            return `https://www.pixiv.net/en/artworks/${url.substring(url.lastIndexOf('/') + 1).replace(/_p\d+\.(png|jpg|jpeg|gif)$/, '')}`;
        }
        return url;
    }
    
    function setProvider(provider) {
        provider = provider.toLowerCase()
        if (providerList.indexOf(provider) !== -1) {
            Persistent.states.booru.provider = provider
            root.addSystemMessage(Translation.tr("Provider set to ") + providers[provider].name
                + (provider == "zerochan" ? Translation.tr(". Notes for Zerochan:\n- You must enter a color\n- Set your zerochan username in `sidebar.booru.zerochan.username` config option. You [might be banned for not doing so](https://www.zerochan.net/api#:~:text=The%20request%20may%20still%20be%20completed%20successfully%20without%20this%20custom%20header%2C%20but%20your%20project%20may%20be%20banned%20for%20being%20anonymous.)!") : ""))
        } else {
            root.addSystemMessage(Translation.tr("Invalid API provider. Supported: \n- ") + providerList.join("\n- "))
        }
    }

    function clearResponses() {
        const previous = root.responses
        root.responses = []
        root._destroyResponsesLater(previous)
    }

    function addSystemMessage(message) {
        root._appendResponse(root.booruResponseDataComponent.createObject(null, {
            "provider": "system",
            "tags": [],
            "page": -1,
            "images": [],
            "message": `${message}`
        }))
    }

    function constructRequestUrlForProvider(providerId, tags, nsfw=true, limit=20, page=1) {
        const resolvedProviderId = providers[providerId] ? providerId : currentProvider
        var provider = providers[resolvedProviderId]
        var baseUrl = provider.api
        var url = baseUrl
        var tagString = tags.join(" ")
        if (!nsfw && !(["zerochan", "waifu.im", "t.alcy.cc"].includes(resolvedProviderId))) {
            if (resolvedProviderId == "gelbooru")
                tagString += " rating:general";
            else 
                tagString += " rating:safe";
        }
        var params = []
        // Tags & limit
        if (resolvedProviderId === "zerochan") {
            params.push("c=" + tagString) // zerochan doesn't have search in api, so we use color
            params.push("l=" + limit)
            params.push("s=" + "fav")
            params.push("t=" + 1)
            params.push("p=" + page)
        }
        else if (resolvedProviderId === "waifu.im") {
            // https://docs.waifu.im/docs/getting-started#filter-by-tags
            // All tags: https://api.waifu.im/tags
            //
            // Tags filter request should be like this:
            // https://api.waifu.im/images?IncludedTags=waifu&IncludedTags=maid
            tags.filter(tag => tag.length > 0).forEach(tag => {
                params.push("IncludedTags=" + encodeURIComponent(tag.toLowerCase()));
            });

            // https://docs.waifu.im/docs/getting-started#pagination
            params.push("PageSize=" + limit)
            params.push("Page=" + page)
            
            // https://docs.waifu.im/docs/getting-started#nsfw-content
            //
            // With this, both SFW and NSFW images will be shown as the default 
            // while only NSFW images will be shown when NSFW mode is enabled,
            // meaning no only SFW mode, so i'll be disabling this.
            // params.push("IsNsfw=" + (nsfw ? "True" : "All")) // All is random
            //
            // Show only SFW images by default and only NSFW images in NSFW mode.
            // Change 'True' to 'All' to see both SFW and NSFW images in NSFW mode.
            if (nsfw) { params.push("IsNsfw=True") }
        }
        else if (resolvedProviderId === "t.alcy.cc") {
            // https://t.alcy.cc/docs.html
            url += "json"
            let tag = tags[0] || "pc" // Alcy only takes one tag as param. Use `pc` as the default one if no tags are provided
            params.push(tag + "=" + limit)
        }
        else {
            params.push("tags=" + encodeURIComponent(tagString))
            params.push("limit=" + limit)
            if (resolvedProviderId == "gelbooru") {
                params.push("pid=" + page)
                const apiKey = String(Config.options?.sidebar?.booru?.gelbooru?.apiKey ?? "").trim()
                const userId = String(Config.options?.sidebar?.booru?.gelbooru?.userId ?? "").trim()
                if (apiKey.length > 0 && userId.length > 0) {
                    params.push("api_key=" + encodeURIComponent(apiKey))
                    params.push("user_id=" + encodeURIComponent(userId))
                }
            }
            else {
                params.push("page=" + page)
            }
        }
        if (baseUrl.indexOf("?") === -1) {
            url += "?" + params.join("&")
        } else {
            url += "&" + params.join("&")
        }
        return url
    }

    function constructRequestUrl(tags, nsfw=true, limit=20, page=1) {
        return root.constructRequestUrlForProvider(currentProvider, tags, nsfw, limit, page)
    }

    function makeRequest(tags, nsfw=false, limit=20, page=1, providerOverride) {
        const requestProviderId = providers[providerOverride] ? providerOverride : currentProvider
        const requestProvider = providers[requestProviderId]
        if (requestProviderId === "gelbooru") {
            const apiKey = String(Config.options?.sidebar?.booru?.gelbooru?.apiKey ?? "").trim()
            const userId = String(Config.options?.sidebar?.booru?.gelbooru?.userId ?? "").trim()
            if (apiKey.length === 0 || userId.length === 0) {
                root.addSystemMessage(Translation.tr("Gelbooru requires an API key and user ID. Add both in Settings → Sidebars → Gelbooru."))
                root.responseFinished()
                return
            }
        }
        var url = root.constructRequestUrlForProvider(requestProviderId, tags, nsfw, limit, page)
        const logUrl = requestProviderId === "gelbooru"
            ? url.replace(/([?&](?:api_key|user_id)=)[^&]*/g, "$1[redacted]")
            : url
        _log("[Booru] Making request to " + logUrl)

        const newResponse = root.booruResponseDataComponent.createObject(null, {
            "provider": requestProviderId,
            "tags": tags,
            "page": page,
            "images": [],
            "message": ""
        })

        var xhr = new XMLHttpRequest()
        xhr.open("GET", url)
        xhr.onreadystatechange = function() {
            if (xhr.readyState === XMLHttpRequest.DONE && xhr.status === 200) {
                try {
                    // console.log("[Booru] Raw response: " + xhr.responseText)
                    let response;
                    if (requestProvider.manualParseFunc) {
                        response = requestProvider.manualParseFunc(xhr.responseText)
                    } else {
                        response = JSON.parse(xhr.responseText)
                        response = requestProvider.mapFunc(response)
                    }
                    // console.log("[Booru] Mapped response: " + JSON.stringify(response))
                    newResponse.images = response
                    newResponse.message = response.length > 0 ? "" : root.failMessage
                    
                } catch (e) {
                    console.log("[Booru] Failed to parse response: " + e)
                    newResponse.message = root.failMessage
                } finally {
                    root.runningRequests--;
                    root._appendResponse(newResponse)
                }
            }
            else if (xhr.readyState === XMLHttpRequest.DONE) {
                console.log("[Booru] Request failed with status: " + xhr.status)
                newResponse.message = root.failMessage
                root.runningRequests--;
                root._appendResponse(newResponse)
            }
            root.responseFinished()
        }

        try {
            // Required for danbooru
            if (requestProviderId == "danbooru") {
                xhr.setRequestHeader("User-Agent", defaultUserAgent)
            }
            else if (requestProviderId == "waifu.im") {
                xhr.setRequestHeader("Accept-Version", "v7")
            }
            else if (requestProviderId == "zerochan") {
                const userAgent = (Config.options?.sidebar?.booru?.zerochan?.username)
                    ? `Desktop sidebar booru viewer - username: ${Config.options?.sidebar?.booru?.zerochan?.username}`
                    : defaultUserAgent
                xhr.setRequestHeader("User-Agent", userAgent)
            }
            root.runningRequests++;
            xhr.send()
        } catch (error) {
            _log("Could not set User-Agent:", error)
        } 
    }

    property var currentTagRequest: null
    function triggerTagSearch(query) {
        if (currentTagRequest) {
            currentTagRequest.abort();
        }

        var provider = providers[currentProvider]
        if (provider.fixedTags) {
            root.tagSuggestion(query, provider.fixedTags)
            return provider.fixedTags;
        } else if (!provider.tagSearchTemplate) {
            return
        }
        var url = provider.tagSearchTemplate.replace("{{query}}", encodeURIComponent(query))
        if (currentProvider === "gelbooru") {
            const apiKey = String(Config.options?.sidebar?.booru?.gelbooru?.apiKey ?? "").trim()
            const userId = String(Config.options?.sidebar?.booru?.gelbooru?.userId ?? "").trim()
            if (apiKey.length === 0 || userId.length === 0)
                return
            url += "&api_key=" + encodeURIComponent(apiKey) + "&user_id=" + encodeURIComponent(userId)
        }

        var xhr = new XMLHttpRequest()
        currentTagRequest = xhr
        xhr.open("GET", url)
        xhr.onreadystatechange = function() {
            if (xhr.readyState === XMLHttpRequest.DONE && xhr.status === 200) {
                currentTagRequest = null
                try {
                    // console.log("[Booru] Raw response: " + xhr.responseText)
                    var response = JSON.parse(xhr.responseText)
                    response = provider.tagMapFunc(response)
                    // console.log("[Booru] Mapped response: " + JSON.stringify(response))
                    root.tagSuggestion(query, response)
                } catch (e) {
                    console.log("[Booru] Failed to parse response: " + e)
                }
            }
            else if (xhr.readyState === XMLHttpRequest.DONE) {
                console.log("[Booru] Request failed with status: " + xhr.status)
            }
        }
        try {
            // Required for danbooru
            if (currentProvider == "danbooru") {
                xhr.setRequestHeader("User-Agent", defaultUserAgent)
            } else if (currentProvider == "waifu.im") {
                xhr.setRequestHeader("Accept-Version", "v7")
            }
            xhr.send()
        } catch (error) {
            _log("Could not set User-Agent:", error)
        } 
    }
}

